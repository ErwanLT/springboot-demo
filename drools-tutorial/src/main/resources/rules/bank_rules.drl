package rules

import fr.eletutour.model.Account
import fr.eletutour.model.Transaction

dialect "mvel"

global org.slf4j.Logger logger

// Règle 0 : Valider les données de la transaction
rule "Validate Transaction"
    salience 100
    when
        $t: Transaction(
            accountNumber == null || accountNumber == "",
            approved == false
            ||
            amount <= 0,
            approved == false
            ||
            type == null || type == "",
            approved == false
        )
    then
        $t.setApproved(false);
        $t.setMessage("Transaction invalide : numéro de compte, montant ou type manquant ou incorrect.");
        logger.error("Règle 'Validate Transaction' déclenchée pour une transaction invalide");
end

// Règle 1 : Vérifier si le retrait est possible
rule "Check Sufficient Balance for Withdrawal"
    salience 50
    when
        $t: Transaction(type in ("WITHDRAW", "WITHDRAWAL"), $amount: amount, approved == false)
        $a: Account(accountNumber == $t.accountNumber, balance >= $amount)
    then
        $t.setApproved(true);
        $a.setBalance($a.getBalance() - $amount);
        $t.setMessage("Retrait de " + $amount + " approuvé.");
        logger.info("Règle 'Check Sufficient Balance' déclenchée pour " + $t.getAccountNumber());
end

// Règle 2 : Refuser le retrait si solde insuffisant
rule "Deny Withdrawal Insufficient Balance"
    salience 50
    when
        $t: Transaction(type in ("WITHDRAW", "WITHDRAWAL"), $amount: amount, approved == false)
        $a: Account(accountNumber == $t.accountNumber, balance < $amount)
    then
        $t.setApproved(false);
        $t.setMessage("Retrait de " + $amount + " refusé : solde insuffisant.");
        logger.info("Règle 'Deny Withdrawal' déclenchée pour " + $t.getAccountNumber() + ", solde=" + $a.getBalance());
end

// Règle 3 : Appliquer des frais de découvert
rule "Apply Overdraft Fee"
    salience 30
    when
        $a: Account(balance < 0)
    then
        double fee = 10.0;
        $a.setBalance($a.getBalance() - fee);
        logger.info("Frais de découvert de " + fee + " appliqués au compte " + $a.getAccountNumber());
end

// Règle 4 : Détecter une transaction suspecte
rule "Flag Suspicious Transaction"
    salience 20
    when
        $t: Transaction(type in ("WITHDRAW", "WITHDRAWAL"), amount > 1000, approved == true)
    then
        $t.setMessage($t.getMessage() + " [ATTENTION : Transaction suspecte détectée !]");
        logger.info("Transaction suspecte : " + $t);
end

// Règle 5 : Gérer les dépôts
rule "Process Deposit"
    salience 50
    when
        $t: Transaction(type == "DEPOSIT", $amount: amount, approved == false)
        $a: Account(accountNumber == $t.accountNumber)
    then
        $t.setApproved(true);
        $a.setBalance($a.getBalance() + $amount);
        $t.setMessage("Dépôt de " + $amount + " approuvé.");
        logger.info("Règle 'Process Deposit' déclenchée pour " + $t.getAccountNumber());
end